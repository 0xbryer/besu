dependencies {
  implementation 'com.github.docker-java:docker-java'
  implementation 'com.squareup.okhttp3:okhttp'
  implementation 'io.vertx:vertx-core'
  implementation 'org.apache.logging.log4j:log4j-api'
  implementation 'org.assertj:assertj-core'

  runtime 'org.apache.logging.log4j:log4j-core'

  testImplementation project(':ethereum:client')
  testImplementation project(':ethereum:jsonrpc')
  testImplementation project(':pantheon')
  testImplementation project(':util')

  testImplementation 'junit:junit'
  testImplementation 'org.awaitility:awaitility'
}

test.enabled = false

task smokeTest(type: Test) {
  mustRunAfter rootProject.subprojects*.test
  description = 'Runs basic Pantheon smoke tests.'
  group = 'verification'
  systemProperty 'pantheon.test.distribution', "${buildDir}/pantheon-${project.version}"
}

task unpackTarDistribution(type: Copy) {
  dependsOn rootProject.distTar
  def tar = file("${projectDir}/../build/distributions/pantheon-${project.version}.tar.gz")
  inputs.files tar
  from tarTree(tar)
  into {buildDir}
}

smokeTest.dependsOn(unpackTarDistribution)
smokeTest.dependsOn(rootProject.installDist)

// Commenting out dependency so that check (on CircleCI won't try to run this and fail)
// Jenkins can run it as a separate task.
//check.dependsOn(smokeTest)
